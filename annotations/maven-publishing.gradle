task sourceJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from "$buildDir/javadoc"
}

afterEvaluate {
    sourceJar.dependsOn assembleRelease
}

bintray {
    user = System.getenv('BINTRAY_USER_ICAPPS')
    key = System.getenv('BINTRAY_KEY_ICAPPS')
    pkg {
        repo = 'maven'
        name = 'icapps-arch-annotations'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/icapps/android-architecture-annotation-processor/tree/master/annotations'

        version {
            name = "$artifactVersion"
            desc = "Android annotations for android projects by icapps"
            released = new Date()
        }
    }
    publications = ['maven']
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "Maarten Van Giel"
            email "maarten.vangiel@icapps.com"
            organization "icapps"
        }
        developer {
            name "Nicola Verbeeck"
            email "nicola.verbeeck@icapps.com"
            organization "icapps"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "$artifactGroup"
            artifactId "$artifactName"
            version "$artifactVersion"

            artifact("$buildDir/outputs/aar/${project.name}-release.aar")
            artifact sourceJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Android annotations for android projects by icapps')
                root.appendNode('name', 'icapps-arch-annotations')
                root.appendNode('url', 'https://github.com/icapps/android-architecture-annotation-processor/tree/master/annotations')
                root.children().last() + pomConfig

                def dependenciesNode = root.appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    if (it instanceof ExternalModuleDependency) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}